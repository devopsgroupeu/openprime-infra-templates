locals {
  helm_charts = {

    # @section karpenter begin
    karpenter = {
      template_values_file = "${path.module}/../../helm/karpenter/values.yaml.tftpl"
      values = {
        cluster_name     = module.eks.cluster_name
        cluster_endpoint = module.eks.cluster_endpoint
        queue_name       = module.karpenter.queue_name
      }
    }
    # @section karpenter end

    # @section promtail begin
    promtail = {
      template_values_file = "${path.module}/../../helm/promtail/values.yaml.tftpl"
      values = {
        logLevel = "info"
      }
    }
    # @section promtail end

    # @section aws_load_balancer_controller begin
    aws_load_balancer_controller = {
      template_values_file = "${path.module}/../../helm/aws-load-balancer-controller/values.yaml.tftpl"
      values = {
        service_account_name = local.aws_lb_service_account_name
        alb_role_arn         = module.alb_controller_irsa_role.iam_role_arn
        cluster_name         = module.eks.cluster_name
        region               = var.region
        vpc_id               = module.vpc.vpc_id
        replica_count        = var.aws_lb_replica_count
        pdb_max_unavailable  = var.aws_lb_pdb_max_unavailable
      }
    }
    # @section aws_load_balancer_controller end

    # @section loki begin
    loki = {
      template_values_file = "${path.module}/../../helm/loki/values.yaml.tftpl"
      values = {
        region               = var.region
        loki_role_arn        = module.loki_role.iam_role_arn
        service_account_name = "loki-sa"
        s3_bucket_chunk      = module.loki_s3_buckets["chunk"].s3_bucket_id
        s3_bucket_ruler      = module.loki_s3_buckets["ruler"].s3_bucket_id
      }
    }
    # @section loki end
  }
}

resource "local_file" "helm_chart_values" {
  for_each = local.helm_charts

  content = templatefile(
    each.value.template_values_file,
    each.value.values
  )
  filename = trimsuffix(each.value.template_values_file, ".tftpl")
}
